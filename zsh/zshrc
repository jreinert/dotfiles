. "$HOME/.config/zsh/env"
. "$XDG_CONFIG_HOME/zsh/aliases"

FZF_DEFAULT_OPTS='-e'
FZF_ALT_C_COMMAND='bdsm -h -l 2> /dev/null'

. /usr/share/fzf/key-bindings.zsh
. /usr/share/fzf/completion.zsh

# Lines configured by zsh-newuser-install
HISTFILE="$XDG_CACHE_HOME/zsh/history"
HISTSIZE=100000
SAVEHIST=100000
setopt sharehistory autocd extendedglob nomatch notify histfindnodups
unsetopt beep
bindkey -v
# End of lines configured by zsh-newuser-install

. "$XDG_CONFIG_HOME/zsh/completion"
. "$XDG_CONFIG_HOME/zsh/functions"
. "$XDG_CONFIG_HOME/zsh/z/z.sh"

# Keybindings {{
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^r' history-incremental-pattern-search-backward
bindkey '^k' history-beginning-search-backward
bindkey '^j' history-beginning-search-forward
bindkey '^p' push-input
bindkey '^ ' fzf-cd-widget
# }}

# Prompt {{
autoload -Uz vcs_info
precmd_functions+=( vcs_info )
setopt prompt_subst
PROMPT='%# %1~%(0?..%F{red})>%f '
RPROMPT=\$vcs_info_msg_0_
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' formats '<%b> [%u%c]'
# }}

# Safe rm {{
alias rm='rm -I '
setopt rm_star_silent
# }}

function ipfs-add() {
	local hash=$(ipfs add -q "$@" | tail -n1)
	local url="https://ipfs.jokke.space/ipfs/$hash"
	echo -n "$url\nipfs get /ipfs/$hash"
	echo "$url" | xclip -i
}

function ipfs-add-crypt() {
	local recipient=$(gpg --list-public-keys | grep 'uid.*@' | sed 's/.*\?\] //' | fzf | sed 's/.*<\(.\+\?\)>.*/\1/')
	local hash=$(tar c "$@" | gpg -ser "$recipient" | ipfs add -q)
	local url="https://ipfs.jokke.space/ipfs/$hash"
	echo -n "$url\ncurl '$url' | gpg -d | tar x\nipfs cat /ipfs/$hash | gpg -d | tar x"
	echo "$url" | xclip -i
}

guessed_weather_location=

case $(readlink /etc/localtime) in
*Berlin)    guessed_weather_location='frankfurt%20am%20main' ;;
*Helsinki)  guessed_weather_location='helsinki' ;;
*Hong_Kong) guessed_weather_location='hongkong' ;;
esac

WEATHER_LOCATION=${WEATHER_LOCATION:-$guessed_weather_location}
timeout 1 curl "wttr.in/$WEATHER_LOCATION?0qM"
echo
task
echo
khal --color list --format "$(echo '{bold}{start-style} - {end-style}{reset}: {cyan}{title}{reset}\n{description}\n--------------------')" today sunday | sed 's/^\(.\{100\}\).\{3,\}/\1.../'
